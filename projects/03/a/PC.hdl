// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/03/a/PC.hdl

/**
 * A 16-bit counter with load and reset control bits.
 * if      (reset[t] == 1) out[t+1] = 0
 * else if (load[t] == 1)  out[t+1] = in[t]
 * else if (inc[t] == 1)   out[t+1] = out[t] + 1  (integer addition)
 * else                    out[t+1] = out[t]
 */

CHIP PC {
    IN in[16],load,inc,reset;
    OUT out[16];

    PARTS:
    // Determine whether we should load a value
    Or(a=load, b=reset, out=loadOrReset);
    Or(a=loadOrReset, b=inc, out=loadOrResetOrInc);

    // out[t+1] = out[t] + 1 if inc[t] == 1
    Add16(a[0]=true, b=value, out=incremented);
    Mux16(a=value, b=incremented, sel=inc, out=value1);

    // out[t+1] = in[t] if load[t] == 1
    Mux16(a=value1, b=in, sel=load, out=value2);

    // out[t+1] = 0 if reset[t] == 1
    Mux16(a=value2, b=false, sel=reset, out=value3);

    Register(in=value3, load=loadOrResetOrInc, out=value, out=out);
}
